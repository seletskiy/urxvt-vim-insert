#!perl -w

use strict;
use File::Temp qw/tempfile/;

sub on_init {
    my($self) = @_;

    $self->{phase} = 'off';
    $self->{paste_data} = '';
}

sub on_tt_paste {
    my($self, $octets) = @_;

    if ($self->{phase} eq 'off') {
        if ($self->is_in_vim()) {
            $self->tt_paste('');
            $self->{paste_data} = $octets;
            $self->{phase} = 'ctrl-r';

            return 1;
        } else {
            return $self->safe_paste($octets);
        }
    }

    ()
}

sub safe_paste {
    my($self, $octets) = @_;

    my ($clipboard_handle, $clipboard_file_name) = tempfile(
        SUFFIX=>'.safe-paste',
        UNLINK=>0,
    );

    $octets =~ s/^\n+//g;

    if ($octets =~ /\n/m) {
        my $command = $self->x_resource('safe-paste.command');
        print $clipboard_handle $octets;
        close($clipboard_handle);

        my $urxvt_pid = $$;
        my $pid = fork;
        if ($pid) {
            waitpid($pid, 0);
            return $!;
        } else {
            exec(($command, $urxvt_pid, $clipboard_file_name));
        }

        return 1
    }

    ()
}

sub on_refresh_end {
    my($self) = @_;

    if ($self->{phase} eq 'ctrl-r') {
        # vim :h i_CTRL-R_CTRL-R
        $self->tt_paste(':if@*==""|set paste|else|call feedkeys("\<C-R>\<C-R>*","n")|endif');
        $self->{phase} = 'paste-mode';
        return;
    }

    if ($self->{phase} eq 'paste-mode') {
        if ($self->is_on_paste()) {
            $self->tt_paste($self->{paste_data});
            $self->{phase} = 'paste-done';
        } else {
            $self->{paste_data} = '';
            $self->{phase} = 'off';
        }

        return;
    }

    if ($self->{phase} eq 'paste-done') {
        $self->tt_paste('');
        $self->{phase} = 'paste-disable';
        return;
    }

    if ($self->{phase} eq 'paste-disable') {
        $self->tt_paste(':set nopaste');
        $self->{phase} = 'off';
        return;
    }
}

sub is_in_vim {
    my($self) = @_;

    my $term = $self->{term};
    my $last_line = $term->ROW_t($term->nrow-1);

    if ($last_line =~ "^-- INSERT --") {
        return 1;
    } else {
        return 0;
    }
}

sub is_on_paste {
    my($self) = @_;

    my $term = $self->{term};
    my $last_line = $term->ROW_t($term->nrow-1);

    if ($last_line =~ '^-- INSERT \(paste\) --') {
        return 1;
    } else {
        return 0;
    }
}
